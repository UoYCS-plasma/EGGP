Main = try ([pickEdge]; markOutputs!; [pickNewTarget]; mutateEdge; unmarkOutputs!)

pickEdge
(
	a, b, c : list
)
[1.0]
[
	(n0, a)
	(n1, b)
	|
	(e0, n0, n1, c)
]
=>
[
	(n0, a#blue)
	(n1, b#green)
	|
	(e0, n0, n1, c#green)
]
interface =
{
	n0, n1
}

markOutputs
(
	a, b, c : list
)
[
	(n0, a)
	(n1, b#blue)
	|
	(e0, n0, n1, c)
]
=>
[
	(n0, a#blue)
	(n1, b#blue)
	|
	(e0, n0, n1, c)
]
interface =
{
	n0, n1
}

pickNewTarget
(
	d : list;
	s : string
)
[1.0]
[
	(n2, d:s)
	|
]
=>
[
	(n2, d:s#red)
	|
]
interface =
{
	n2
}
where s != "OUT"

mutateEdge
(
	a, b, c, d : list
)
[
	(n0, a#blue)
	(n1, b#green)
	(n2, d#red)
	|
	(e0, n0, n1, c#green)
]
=>
[
	(n0, a)
	(n1, b)
	(n2, d)
	|
	(e0, n0, n2, c)
]
interface =
{
	n0, n1, n2
}

unmarkOutputs
(
	a : list
)
[
	(n0, a#any)
	|
]
=>
[
	(n0, a)
	|
]
interface =
{
	n0
}
